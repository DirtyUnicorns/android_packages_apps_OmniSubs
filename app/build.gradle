/*
 * Copyright (c) 2016-2017 Projekt Substratum
 * This file is part of Substratum.
 *
 * Substratum is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * Substratum is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with Substratum.  If not, see <http://www.gnu.org/licenses/>.
 */

apply plugin: 'com.android.application'

def gitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.0"

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "substratum_beta_${variant.versionCode}_${gitHash()}.apk"
        }
    }

    defaultConfig {
        applicationId "org.omnirom.substratum"
        minSdkVersion 24
        targetSdkVersion 26
        versionCode 815
        versionName "eight hundred fifteen"
        buildConfigField "java.util.Date", "buildTime", "new java.util.Date(" + System.currentTimeMillis() + "L)"
        buildConfigField "String", "GIT_HASH", "\"${gitHash()}\""
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "preview"
    productFlavors {
        main {}
        preview {
            targetSdkVersion 26
            versionNameSuffix " (target-preview)"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dexOptions {
        javaMaxHeapSize '2048m'
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    // Android Support Libraries
    compile 'com.android.support:appcompat-v7:26.0.0'
    compile 'com.android.support:cardview-v7:26.0.0'
    compile 'com.android.support:recyclerview-v7:26.0.0'
    compile 'com.android.support:support-v13:26.0.0'
    compile 'com.android.support:design:26.0.0'
    compile 'com.android.support:palette-v7:26.0.0'
    compile 'com.android.support:preference-v14:26.0.0'
    // APK Signer
    compile files('src/main/libs/apksig-2.3.0.jar')
    compile files('src/main/libs/bcprov-jdk16-1.45.jar')
    // Direct Interface to the Overlay Manager Service
    compile files('src/main/libs/overlay-manager-service.jar')
}